set(PACKAGE_VERSION "@LeapSerial_VERSION@")
set(PACKAGE_VERSION_MAJOR "@LeapSerial_VERSION_MAJOR@")
set(PACKAGE_VERSION_MINOR "@LeapSerial_VERSION_MINOR@")
set(PACKAGE_VERSION_PATCH "@LeapSerial_VERSION_PATCH@")

if(LeapSerial_DEBUG)
  message(STATUS "Debug mode on")
  message(STATUS "Installed LeapSerial_VERSION: ${PACKAGE_VERSION}")

  message(STATUS "Installed LeapSerial_BUILD_ARCHITECTURES: @LeapSerial_BUILD_ARCHITECTURES@")
  message(STATUS "Configured LeapSerial_BUILD_ARCHITECTURES: ${LeapSerial_BUILD_ARCHITECTURES}")

  message(STATUS "Installed CMAKE_SIZEOF_VOID_P: @CMAKE_SIZEOF_VOID_P@")
  message(STATUS "Configured CMAKE_SIZEOF_VOID_P: ${CMAKE_SIZEOF_VOID_P}")
endif()

# If the customer has an override architecture requirement, use that
if(DEFINED LeapSerial_ARCHITECTURE)
  foreach(i IN LISTS LeapSerial_ARCHITECTURE)
    string(REGEX MATCH "amd64|x86_64|x64" LeapSerial_is_x64 ${i})
    string(REGEX MATCH "i386|x86" LeapSerial_is_x86 ${i})
    string(REGEX MATCH "arm" LeapSerial_is_arm ${i})

    # Classify
    if(LeapSerial_is_x86)
      list(APPEND LeapSerial_ARCHITECTURES x86)
    elseif(LeapSerial_is_x64)
      list(APPEND LeapSerial_ARCHITECTURES x64)
    elseif(LeapSerial_is_arm)
      list(APPEND LeapSerial_ARCHITECTURES arm)
    else()
      message(WARNING "Unrecognized architecture ${i}")
    endif()
  endforeach()
else()
  # Try to infer what the user wants
  if(CMAKE_SYSTEM_PROCESSOR STREQUAL "arm")
    set(LeapSerial_ARCHITECTURES arm)
  elseif(CMAKE_SIZEOF_VOID_P EQUAL "8")
    set(LeapSerial_ARCHITECTURES x64)
  else()
    set(LeapSerial_ARCHITECTURES x86)
  endif()
endif()

if(LeapSerial_DEBUG)
  message(STATUS "LeapSerial_ARCHITECTURE: ${LeapSerial_ARCHITECTURE}")
  message(STATUS "LeapSerial_ARCHITECTURES: ${LeapSerial_ARCHITECTURES}")
endif()

# Architectures we were built with
set(LeapSerial_BUILD_ARCHITECTURES @LeapSerial_BUILD_ARCHITECTURES@)
if(LeapSerial_DEBUG)
  message(STATUS "LeapSerial_BUILD_ARCHITECTURES: ${LeapSerial_BUILD_ARCHITECTURES}")
endif()

# Verify that we match all of the architectures requested by the customer
foreach(i IN LISTS LeapSerial_ARCHITECTURES)
  list(FIND LeapSerial_BUILD_ARCHITECTURES ${i} found)
  if(found EQUAL -1)
    set(PACKAGE_VERSION_COMPATIBLE FALSE)
    set(PACKAGE_VERSION_UNSUITABLE TRUE)
    if(LeapSerial_DEBUG)
      message(STATUS "Requested architecture of ${LeapSerial_ARCHITECTURE} not compatible with @LeapSerial_BUILD_ARCHITECTURES@")
    endif()
    return()
  endif()
endforeach()

# Determine whether the user's request (either implied or explicit) for libstdc++ can
# be met by this verison of LeapSerial
if(CMAKE_CXX_COMPILER_ID STREQUAL "AppleClang")
  if(LeapSerial_DEBUG)
    message(STATUS "Installed LeapSerial_USE_LIBCXX: @LeapSerial_USE_LIBCXX@")
    message(STATUS "Configured LeapSerial_USE_LIBCXX: ${LeapSerial_USE_LIBCXX}")
  endif()
endif()

if(PACKAGE_VERSION VERSION_LESS PACKAGE_FIND_VERSION)
  if(LeapSerial_DEBUG)
    message(STATUS "Our version is less than the requested one, trivially incompatible")
  endif()
  set(PACKAGE_VERSION_COMPATIBLE FALSE)
elseif(NOT PACKAGE_FIND_VERSION_MINOR EQUAL PACKAGE_VERSION_MINOR)
  if(LeapSerial_DEBUG)
    message(STATUS "Minor version mismatch, requested minor is ${PACKAGE_FIND_VERSION_MINOR}, this version is ${PACKAGE_VERSION_MINOR}")
  endif()
  set(PACKAGE_VERSION_COMPATIBLE FALSE)
elseif(NOT PACKAGE_FIND_VERSION_MAJOR EQUAL PACKAGE_VERSION_MAJOR)
  if(LeapSerial_DEBUG)
    message(STATUS "Major version mismatch, failure.")
  endif()
  set(PACKAGE_VERSION_COMPATIBLE FALSE)
else()
  set(PACKAGE_VERSION_COMPATIBLE TRUE)
  if (PACKAGE_VERSION VERSION_EQUAL PACKAGE_FIND_VERSION)
    set(PACKAGE_VERSION_EXACT TRUE)
  endif()

  if(LeapSerial_DEBUG)
    message(STATUS "Using LeapSerial in ${CMAKE_CURRENT_SOURCE_DIR}")
  endif()
endif()
